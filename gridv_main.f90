PROGRAM GRIDV
!
!     VERSION 94 Revision A
!
!     GRIDV CUTS A PLANE FROM THREE SPACE AND CALCULATES
!     THE DESIRED FUNCTION AT POINTS ON A GRID OVERLAID UPON
!     THE PLANE.  THE POSSIBLE FUNCTIONS ARE:
!     1   RHO
!     2   DEL**2(RHO)
!     3   KE (G)
!
!     OTHERS ARE EASILY ADDED ...
!
!     GRIDV IS A MODIFIED VERSION OF GRID.
!
!     GRID TO GRIDV BY T.A. KEITH, 1991
!
!     GRIDV CAN HANDLE S,P,D(6) AND F(10) GAUSSIAN FUNCTIONS
!     MODIFIED TO INCLUDE F-FUNCTIONS BY RICHARD G. A. BONE, 1993
!
!     Cleaned Up Jan. 1994.  TAK
!
!     QUESTIONS AND SUGGESTIONS SHOULD BE DIRECTED TO:
!     T.A. KEITH:  keith@babbage.chemistry.mcmaster.ca
!     or
!     R.F.W. Bader:  bader@mcmail.cis.mcmaster.ca
!
!     TO REDIMENSION GRIDV TO HANDLE MORE MO's, PRIMITIVES AND NUCLEI
!     CHANGE THE VALUES MMO, MPRIMS AND MCENT IN THE PARAMETER
!     STATEMENTS.  TO CHANGE THE MAXIMUM GRID SIZE, CHANGE THE PARAMETER
!     MPts.
!
   USE grid_data
   USE molecular_data
   USE string_data
   USE io_units
   USE computation_data
   implicit none
   
   ! Local variables
   character(len=80) :: LINE
   character(len=8) :: WORD
   character(len=40) :: WINP, WOUT, WFN, WDBG
   character(len=4) :: FINP = '.inf', FOUT = '.grd', FWFN = '.wfn'
   real(8) :: A(3,3), CX(3), E(3), CT(3)
   real(8) :: XS(3,MCENT), X(3,MCENT), XY(4)
   integer :: IR(MCENT)
   integer :: LEVEL = 1, NAE = 99, ISRF = 2
   integer :: ILEN, I, J, K, L, NARG, NUMBER, NUM
   integer :: IEG, IFUNC, LPST, NPLAN, NX, INDX
   real(8) :: DEC, DNUM, XX, YY, ZZ
   character(len=80) :: TITLE
   
   ! Initialize io unit numbers
   INPT = 20
   IOUT = 30
   IWFN = 10
!
   CALL MAKNAME(1,WINP,ILEN,FINP)
   IF (ILEN .EQ. 0) STOP 'usage: gridv inffile wfnfile '
   CALL MAKNAME(2,WFN,ILEN,FWFN)
   IF (ILEN .EQ. 0) STOP 'usage: gridv inffile wfnfile '
!     Generate output filename from input filename
   CALL GETARG(1,WOUT)
   CALL MAKNAME_DIRECT(WOUT,ILEN,FOUT)
!     CALL MAKNAME(1,WDBG,ILEN,FDBG)
!
   OPEN (INPT,FILE=WINP)
   OPEN (IOUT,FILE=WOUT,FORM='FORMATTED')
   OPEN (IWFN,FILE=WFN)
!     OPEN (IDBG,FILE=WDBG)
!
!    read WAVEFUNCTION FILE
!
   CALL RDPSI
!
!    INPUT PARAMETERS FOR GRIDV
!
!    READS ARE DONE USING THE FUNCTION CALL NUMBER UPON THE
!    INPUT STRING.  LPST IS THE POSITION FROM WHICH THE READ
!    IS DONE.  ITS VALUE IS UPDATED ONCE THE STRING IS FOUND.
!    THE NUMBER IF RETURNED BOTH AS AN INTEGER (INUM) AND
!    REAL*8 (DNUM).  ONE NEED MERELY REPLACE INUM OR DNUM WITH
!    THE VARIABLE DESIRED TO BE READ.  THE FUNCTION RETURNS A
!    VALUE OF ZERO IF SUCCESSFUL FINDING THE STRING OF INTEREST
!    AND A VALUE OF ONE IF AN ERROR CONDITION OCCURED.
!
!             NUMBER(STRING,LPST,INUM,DNUM)
!
!     UNFORMATTED READ CODE IS DUE TO DR. SIMON K KEARSLEY
!
   read(INPT,1600) TITLE
!
!    PLOT:   INPUT GRID EDGE SIZE (IN AU) AND INCREMENT.  THIS
!    EFFECTIVELY DESCIBES THE COARSENESS OF THE GRID.  THE
!    SAMPLING IS DONE AT (XY(1)/XY(2))**2) POINTS WITHIN
!    THE PLANE
!
   read (20,200) LINE
   LPST = 8
   DO 100 I = 1,2
      IF (NUMBER(LINE,LPST,NUM,XY(I)) .GT. 0) GOTO 990
100 CONTINUE
!
   NX = IDINT(XY(1)/XY(2))
   CALL allocate_grid(NX)
!
   read (20,200) LINE
   LPST = 8
   DO 110 I = 1,3
      IF (NUMBER(LINE,LPST,NUM,CX(I)) .GT. 0) GOTO 992
110 CONTINUE
!
!    PLAN:  DEFINE PLANE THAT IS DESIRED
!    THE REQUIRED ROTATION MATRIX IS GENERATED BY FINDING THE
!    INERTIAL AXIS OF THE ATOMS INPUT IN IR.  TO DEFINE THE PLANE
!    OF INTEREST, ONE NEED ONLY INCLUDE THOSE ATOMS THAT DEFINE
!    THE PLANE.  IF THE PLANE IS DEFINED BY MORE THAN THREE
!    ATOMS,  IT IS BEST TO USE DUMMY ATOM INPUT.  DUMMY ATOMS
!    CAN BE EITHER UNIT VECTORS ALONG COORDINATE AXIS OR
!    COORDINATE POSITIONS IN SPACE.  TO INPUT A DUMMY ATOM,
!    INPUT A NEGATIVE INTEGER INTO IR.  THE ABSOLUTE VALUE OF
!    THE INTEGER WILL CORRESPOND TO WHICH OF THE FOLLOWING DUMMY
!    LINES CORRESPONDS TO THE DESCRIBED ATOM.  FOR EXAMPLE
!    ONE MAY INPUT THE FOLLOWING:
!  PLANE:  1  2 -1
!  DUMMY:  1.0 0.0 0.0
!    THIS WILL PLACE THE XZ PLANE

!    MORE THAN THREE ATOMS OR DUMMY ATOMS CAN BE INPUT IF
!    DESIRED, BUT IT IS RARELY NECESSARY...
!    CODE FOR GENERATION OF INERTIAL AXIS IS DUE TO DR. SIMON K
!    KEARSLEY
!
   read (20,200) LINE
   LPST = 8
   IF (NUMBER(LINE,LPST,IEG,DNUM) .GT. 0) GOTO 1100
   IF (IEG .EQ. 0) THEN
      L = 1
120   IF (NUMBER(LINE,LPST,IR(L),DNUM) .GT. 0) GOTO 10
      L = L + 1
      GOTO 120
!
!    DECIDE IF DUMMY ATOMS WERE USED AND THEN READ IN THEIR
!    COORDINATES TO XS(3,N)
!
10    DO 130 I = 1,L
         IF (IR(I) .LT. 0) THEN
            LPST = 8
            read (20,200) LINE
            DO 140 K = 1,3
               IF (NUMBER(LINE,LPST,NUM,XS(K,ABS(IR(I)))) .GT. 0) GOTO 996
140         CONTINUE
         END IF
130   CONTINUE
!
!    GENERATE REQUIRED ROTATION MATRIX
!
      DO 160 J = 1,NCENT
         X(1,J) = XC(J)
         X(2,J) = YC(J)
         X(3,J) = ZC(J)
160   CONTINUE
!
      NPLAN = NCENT
      IF (NCENT.EQ.2) NPLAN=3
      CALL GROCKLE(NPLAN,X,IR,XS,A)
!
   ELSE IF (IEG .EQ. 1) THEN
!
      DO 165 J = 1,3
         IF (NUMBER(LINE,LPST,NUM,E(J)) .GT. 0) GOTO 1110
165   CONTINUE
!
      CALL EULER (E,A)
!
   END IF
!
!    read IN DESIRED FUNCTION
!
   LPST = 8
   read (20,200) LINE
   IF (NUMBER(LINE,LPST,IFUNC,DNUM) .GT. 0) GOTO 998
!
!
!    CALL ROUTINE TO CALCULATE DESIRED FUNCTION
!
   IF(IFUNC .EQ. 1)&
   &CALL GRDRHO (A,CX,XY)
!
   IF(IFUNC .EQ. 2)&
   &CALL GRDD2R (A,CX,XY)
!
   IF(IFUNC .EQ. 3)&
   &CALL GRDKEG (A,CX,XY)
!
!    OUTPUT GRID INFORMATION TO UNIT 30.
!
   NX = IDINT(XY(1)/XY(2))
   CT(1) = A(1,1)*CX(1) + A(2,1)*CX(2) + A(3,1)*CX(3)
   CT(2) = A(1,2)*CX(1) + A(2,2)*CX(2) + A(3,2)*CX(3)
   CT(3) = A(1,3)*CX(1) + A(2,3)*CX(2) + A(3,3)*CX(3)
!
   WRITE (IOUT,'(A)') TRIM(WFNTTL)
   WRITE (IOUT,'(A)') ' '
   WRITE (IOUT,*) NX,NX,XY(1),XY(2)
   WRITE (IOUT,*) IFUNC,NCENT
   WRITE (IOUT,*) (CT(I),I=1,3)
   DO 180 J = 1,NCENT
      XX = A(1,1)*XC(J)+A(2,1)*YC(J)+A(3,1)*ZC(J)-CT(1)
      YY = A(1,2)*XC(J)+A(2,2)*YC(J)+A(3,2)*ZC(J)-CT(2)
      ZZ = A(1,3)*XC(J)+A(2,3)*YC(J)+A(3,3)*ZC(J)-CT(3)
      WRITE (IOUT,*) XX,YY,ZZ
180 CONTINUE
!
   DO 190 J = 1,NX
      INDX = (J-1)*NX
      WRITE (IOUT,*) (GRD(INDX+I),I=1,NX)
190 CONTINUE
!
   CALL cleanup_grid()
   STOP
!
!  INPUT ERROR CODES
980 FORMAT(A70)
990 WRITE (6,991)
991 FORMAT(' ERROR IN PLOT ATTRIBUTES CARD ')
   GOTO 1010
992 WRITE (6,993)
993 FORMAT(' ERROR IN PLOT CENTER CARD ')
   GOTO 1010
994 WRITE (6,995)
995 FORMAT(' ERROR IN ATOMS THAT DEFINE PLANE ')
   GOTO 1010
996 WRITE (6,997)
997 FORMAT(' ERROR IN DUMMY ATOM INPUT CARD ')
   GOTO 1010
998 WRITE (6,999)
999 FORMAT(' ERROR IN FUNCTION INPUT CARD ')
   GOTO 1010
1100 WRITE (6,1105)
1105 FORMAT(' ERROR IN CHOICE OF ORIENTATION PROCEDURE ')
   GOTO 1010
1110 WRITE (6,1115)
1115 FORMAT(' ERROR IN EULER ANGLE ')
1010 STOP
!
!  FORMATS
200 FORMAT(A80)
1600 FORMAT(7X,A70)
END