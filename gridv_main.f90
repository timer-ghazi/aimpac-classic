PROGRAM GRIDV
!
!     VERSION 94 Revision A
!
!     GRIDV CUTS A PLANE FROM THREE SPACE AND CALCULATES
!     THE DESIRED FUNCTION AT POINTS ON A GRID OVERLAID UPON
!     THE PLANE.  THE POSSIBLE FUNCTIONS ARE:
!     1   RHO
!     2   DEL**2(RHO)
!     3   KE (G)
!
!     OTHERS ARE EASILY ADDED ...
!
!     GRIDV IS A MODIFIED VERSION OF GRID.
!
!     GRID TO GRIDV BY T.A. KEITH, 1991
!
!     GRIDV CAN HANDLE S,P,D(6) AND F(10) GAUSSIAN FUNCTIONS
!     MODIFIED TO INCLUDE F-FUNCTIONS BY RICHARD G. A. BONE, 1993
!
!     Cleaned Up Jan. 1994.  TAK
!
!     QUESTIONS AND SUGGESTIONS SHOULD BE DIRECTED TO:
!     T.A. KEITH:  keith@babbage.chemistry.mcmaster.ca
!     or
!     R.F.W. Bader:  bader@mcmail.cis.mcmaster.ca
!
!     TO REDIMENSION GRIDV TO HANDLE MORE MO's, PRIMITIVES AND NUCLEI
!     CHANGE THE VALUES MMO, MPRIMS AND MCENT IN THE PARAMETER
!     STATEMENTS.  TO CHANGE THE MAXIMUM GRID SIZE, CHANGE THE PARAMETER
!     MPts.
!
   USE grid_data
   USE molecular_data
   USE string_data
   USE io_units
   USE computation_data
   implicit none
   
   ! Local variables
   character(len=80) :: LINE
   character(len=8) :: WORD
   character(len=40) :: WINP, WOUT, WFN, WDBG
   character(len=4) :: FINP = '.inf', FOUT = '.grd', FWFN = '.wfn'
   real(8) :: A(3,3), CX(3), E(3), CT(3)
   real(8) :: XS(3,MCENT), X(3,MCENT), XY(4)
   integer :: IR(MCENT)
   integer :: LEVEL = 1, NAE = 99, ISRF = 2
   integer :: ILEN, I, J, K, L, NARG, NUMBER, NUM
   integer :: IEG, IFUNC, LPST, NPLAN, NX, INDX
   real(8) :: DEC, DNUM, XX, YY, ZZ
   character(len=80) :: TITLE
   
   ! Initialize io unit numbers
   INPT = 20
   IOUT = 30
   IWFN = 10
!
   CALL MAKNAME(1,WINP,ILEN,FINP)
   IF (ILEN .EQ. 0) STOP 'usage: gridv inffile wfnfile '
   CALL MAKNAME(2,WFN,ILEN,FWFN)
   IF (ILEN .EQ. 0) STOP 'usage: gridv inffile wfnfile '
!     Generate output filename from input filename
   CALL GETARG(1,WOUT)
   CALL MAKNAME_DIRECT(WOUT,ILEN,FOUT)
!     CALL MAKNAME(1,WDBG,ILEN,FDBG)
!
   OPEN (INPT,FILE=WINP)
   OPEN (IOUT,FILE=WOUT,FORM='FORMATTED')
   OPEN (IWFN,FILE=WFN)
!     OPEN (IDBG,FILE=WDBG)
!
!    read WAVEFUNCTION FILE
!
   CALL RDPSI
!
!    INPUT PARAMETERS FOR GRIDV
!
!    READS ARE DONE USING THE FUNCTION CALL NUMBER UPON THE
!    INPUT STRING.  LPST IS THE POSITION FROM WHICH THE READ
!    IS DONE.  ITS VALUE IS UPDATED ONCE THE STRING IS FOUND.
!    THE NUMBER IF RETURNED BOTH AS AN INTEGER (INUM) AND
!    REAL*8 (DNUM).  ONE NEED MERELY REPLACE INUM OR DNUM WITH
!    THE VARIABLE DESIRED TO BE READ.  THE FUNCTION RETURNS A
!    VALUE OF ZERO IF SUCCESSFUL FINDING THE STRING OF INTEREST
!    AND A VALUE OF ONE IF AN ERROR CONDITION OCCURED.
!
!             NUMBER(STRING,LPST,INUM,DNUM)
!
!     UNFORMATTED READ CODE IS DUE TO DR. SIMON K KEARSLEY
!
   read(INPT,1600) TITLE
!
!    PLOT:   INPUT GRID EDGE SIZE (IN AU) AND INCREMENT.  THIS
!    EFFECTIVELY DESCIBES THE COARSENESS OF THE GRID.  THE
!    SAMPLING IS DONE AT (XY(1)/XY(2))**2) POINTS WITHIN
!    THE PLANE
!
   read (20,200) LINE
   LPST = 8
   do i = 1, 2
      if (number(line,lpst,num,xy(i)) > 0) then
         write (6,'(a)') ' ERROR IN PLOT ATTRIBUTES CARD '
         stop
      end if
   end do
!
   NX = IDINT(XY(1)/XY(2))
   CALL allocate_grid(NX)
!
   read (20,200) LINE
   LPST = 8
   do i = 1, 3
      if (number(line,lpst,num,cx(i)) > 0) then
         write (6,'(a)') ' ERROR IN PLOT CENTER CARD '
         stop
      end if
   end do
!
!    PLAN:  DEFINE PLANE THAT IS DESIRED
!    THE REQUIRED ROTATION MATRIX IS GENERATED BY FINDING THE
!    INERTIAL AXIS OF THE ATOMS INPUT IN IR.  TO DEFINE THE PLANE
!    OF INTEREST, ONE NEED ONLY INCLUDE THOSE ATOMS THAT DEFINE
!    THE PLANE.  IF THE PLANE IS DEFINED BY MORE THAN THREE
!    ATOMS,  IT IS BEST TO USE DUMMY ATOM INPUT.  DUMMY ATOMS
!    CAN BE EITHER UNIT VECTORS ALONG COORDINATE AXIS OR
!    COORDINATE POSITIONS IN SPACE.  TO INPUT A DUMMY ATOM,
!    INPUT A NEGATIVE INTEGER INTO IR.  THE ABSOLUTE VALUE OF
!    THE INTEGER WILL CORRESPOND TO WHICH OF THE FOLLOWING DUMMY
!    LINES CORRESPONDS TO THE DESCRIBED ATOM.  FOR EXAMPLE
!    ONE MAY INPUT THE FOLLOWING:
!  PLANE:  1  2 -1
!  DUMMY:  1.0 0.0 0.0
!    THIS WILL PLACE THE XZ PLANE

!    MORE THAN THREE ATOMS OR DUMMY ATOMS CAN BE INPUT IF
!    DESIRED, BUT IT IS RARELY NECESSARY...
!    CODE FOR GENERATION OF INERTIAL AXIS IS DUE TO DR. SIMON K
!    KEARSLEY
!
   read (20,200) LINE
   LPST = 8
   if (number(line,lpst,ieg,dnum) > 0) then
      write (6,'(a)') ' ERROR IN CHOICE OF ORIENTATION PROCEDURE '
      stop
   end if
   
   if (ieg == 0) then
      l = 1
      do while (number(line,lpst,ir(l),dnum) == 0)
         l = l + 1
      end do
      
      ! Decide if dummy atoms were used and then read in their coordinates to xs(3,n)
      do i = 1, l
         if (ir(i) < 0) then
            lpst = 8
            read (20,200) line
            do k = 1, 3
               if (number(line,lpst,num,xs(k,abs(ir(i)))) > 0) then
                  write (6,'(a)') ' ERROR IN DUMMY ATOM INPUT CARD '
                  stop
               end if
            end do
         end if
      end do
!
!    GENERATE REQUIRED ROTATION MATRIX
!
      do j = 1, ncent
         x(1,j) = xc(j)
         x(2,j) = yc(j)
         x(3,j) = zc(j)
      end do
!
      NPLAN = NCENT
      IF (NCENT.EQ.2) NPLAN=3
      CALL GROCKLE(NPLAN,X,IR,XS,A)
!
   else if (ieg == 1) then
      do j = 1, 3
         if (number(line,lpst,num,e(j)) > 0) then
            write (6,'(a)') ' ERROR IN EULER ANGLE '
            stop
         end if
      end do
      
      call euler (e,a)
   end if
!
!    read IN DESIRED FUNCTION
!
   LPST = 8
   read (20,200) line
   if (number(line,lpst,ifunc,dnum) > 0) then
      write (6,'(a)') ' ERROR IN FUNCTION INPUT CARD '
      stop
   end if
!
!
!    CALL ROUTINE TO CALCULATE DESIRED FUNCTION
!
   IF(IFUNC .EQ. 1)&
   &CALL GRDRHO (A,CX,XY)
!
   IF(IFUNC .EQ. 2)&
   &CALL GRDD2R (A,CX,XY)
!
   IF(IFUNC .EQ. 3)&
   &CALL GRDKEG (A,CX,XY)
!
!    OUTPUT GRID INFORMATION TO UNIT 30.
!
   NX = IDINT(XY(1)/XY(2))
   CT(1) = A(1,1)*CX(1) + A(2,1)*CX(2) + A(3,1)*CX(3)
   CT(2) = A(1,2)*CX(1) + A(2,2)*CX(2) + A(3,2)*CX(3)
   CT(3) = A(1,3)*CX(1) + A(2,3)*CX(2) + A(3,3)*CX(3)
!
   WRITE (IOUT,'(A)') TRIM(WFNTTL)
   WRITE (IOUT,'(A)') ' '
   WRITE (IOUT,*) NX,NX,XY(1),XY(2)
   WRITE (IOUT,*) IFUNC,NCENT
   WRITE (IOUT,*) (CT(I),I=1,3)
   do j = 1, ncent
      xx = a(1,1)*xc(j) + a(2,1)*yc(j) + a(3,1)*zc(j) - ct(1)
      yy = a(1,2)*xc(j) + a(2,2)*yc(j) + a(3,2)*zc(j) - ct(2)
      zz = a(1,3)*xc(j) + a(2,3)*yc(j) + a(3,3)*zc(j) - ct(3)
      write (iout,*) xx,yy,zz
   end do
!
   do j = 1, nx
      indx = (j-1)*nx
      write (iout,*) (grd(indx+i),i=1,nx)
   end do
!
   CALL cleanup_grid()
   STOP
!
!  FORMATS
200 FORMAT(A80)
1600 FORMAT(7X,A70)
END